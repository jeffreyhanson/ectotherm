      Subroutine Rytrec

c    This subroutine computes the configuration factor between two  
c    finite rectangles with a common side at right angles to each other.
c    Equations from Hottel, 1931;
c    Hottel, H.C., 1931, "Radiant heat transmission between surfaces 
c    separated by non-absorbing media," Trans. ASME, vol. 53, 
c    FSP-53-196, pp. 265-273.
c    and from Hamilton and Morgan 
c    Hamilton, D.C. and Morgan, W.R., 1952, "Radiant-interchange 
c    configuration factors," NASA TN 2836. 
                        
c     One of the classic compilations of configuration factors. Has a few typographical
c     errors [see, e.g., Feingold (1966), Feingold and Gupta (1970), and Byrd.] Catalogs
c     twelve different differential area to finite area factors, five differential strip to finite
c     area factors, and eleven finite area to finite area factors. Some of the factors are
c     generated by configuration factor algebra from a smaller set of calculated or
c     derived factors. This is a pioneering work in cataloguing useful information.
c    (from Howell's web page)
c    http://www.me.utexas.edu/~howell/

c    The width (from the common edge to the far edge) of surface 2 is "a"
c    The common edge is length b
c    The width of surface 1 is "c"
c    F12 = Fca

      Implicit none
      Real a,b,c,F,pi,x,y,h,w,hsq,wsq,w2,h2,hpw,grp1,grp2,grp3,grp4
       Real grp5

      Common/rectngl/a,b,c,F

      pi = 3.14159265

C    Width of body = a 
C    Length of body & wing (parallel to long axis of body) = b
C    Width of wing (from junction w body to wing tip) = c
      x=a/b
      y=c/b
c    using the notation from Howell
      h = x
      w = y
c    Defining some common groups
      hsq = h**2
      wsq = w**2
      h2 = 1.+hsq
      w2 = 1.+wsq
      hpw = hsq + wsq
      
c    Defining subgroups of the governing equation
      grp1 = 1./(w*pi)
      grp2 = w*atan(1./w)    + h*atan(1./h)-sqrt(hpw)*atan(sqrt(1./hpw))
      grp3 = (w2*h2)/(w2+h**2)
      grp4 = (w**2 *(w2+h**2))/(w2*hpw)
      grp5 = (h**2 *(h2+w**2))/(h2*hpw)
      
c    Governing equation
      F = grp1*(grp2 + 0.25*log(grp3*(grp4**wsq)*(grp5**hsq)))
      Return
      end